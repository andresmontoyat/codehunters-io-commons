plugins {
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.sonarqube' version '3.3'
    id 'maven-publish'
    id 'jacoco'
    id 'java'
}

allprojects {
    group = 'io.codehunters.commons'

    repositories {
        mavenLocal()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'maven-publish'
    apply plugin: 'jacoco'

    sourceCompatibility = '11'
    targetCompatibility = '11'

    dependencies {
        implementation 'ch.qos.logback:logback-classic'
        implementation 'ch.qos.logback:logback-core'
        implementation 'org.slf4j:slf4j-api'
        implementation 'org.springframework:spring-context'
        implementation 'org.springframework:spring-core'
        implementation 'org.springframework:spring-context-support'
        implementation 'org.projectlombok:lombok:1.18.22'
        implementation 'org.mapstruct:mapstruct:1.5.0.Beta2'

        annotationProcessor 'org.projectlombok:lombok:1.18.22'
        annotationProcessor "org.mapstruct:mapstruct-processor:1.5.0.Beta2"
        testImplementation 'junit:junit'
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${spring_boot_version}")
        }
    }

    publishing {

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("${github_packages_url}/codehuntersio-java-commons-support")
                credentials {
                    username = System.getenv("GH_PACKAGES_USERNAME")
                    password = System.getenv("GH_PACKAGES_TOKEN")
                }
            }
        }

        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    sonarqube {
        properties {
            property 'sonar.sources', 'src/main/java'
            property 'sonar.exclusions', '**/*Test*/**'
        }
    }

    jacocoTestCoverageVerification {
        violationRules {
            rule {
                limit {
                    counter = 'LINE'
                    value = 'COVEREDRATIO'
                    minimum = 1.0
                }
            }
        }
    }

    test {
        useJUnitPlatform()
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

}

task version() {
    doLast {
        println project.version
    }
}