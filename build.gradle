plugins {
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'com.jfrog.artifactory' version '4.11.0'
    id 'org.sonarqube' version '2.7'
    id 'maven-publish'
    id 'java'
}

allprojects {
    group = 'io.codehunters.commons'

    repositories {
        mavenLocal()
        jcenter()
        /*maven {
            url = "${artifactory_url}/libs-release"
            credentials {
                username = "${artifactory_username}"
                password = "${artifactory_password}"
            }
        }
        maven {
            url = "${artifactory_url}/libs-snapshot"
            credentials {
                username = "${artifactory_username}"
                password = "${artifactory_password}"
            }
        }
        maven {
            url = "${artifactory_url}/codehunters-repository"
            credentials {
                username = "${artifactory_username}"
                password = "${artifactory_password}"
            }
        }*/
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'com.jfrog.artifactory'
    apply plugin: 'maven-publish'

    sourceCompatibility = '1.8'

    dependencies {
        implementation 'ch.qos.logback:logback-classic'
        implementation 'ch.qos.logback:logback-core'
        implementation 'org.slf4j:slf4j-api'
        implementation 'org.springframework:spring-context'
        implementation 'org.springframework:spring-core'
        implementation 'org.springframework:spring-context-support'
        implementation 'joda-time:joda-time:2.10.5'
        implementation 'org.projectlombok:lombok:1.18.10'
        implementation 'org.mapstruct:mapstruct:1.3.1.Final'

        annotationProcessor 'org.projectlombok:lombok:1.18.10'
        annotationProcessor "org.mapstruct:mapstruct-processor:1.3.1.Final"

        testCompile 'junit:junit'
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${spring_boot_version}")
        }
    }

    artifactory {
        contextUrl = "${artifactory_url}"
        publish {
            repository {
                repoKey = "${artifactory_repository_key}"
                username = "${artifactory_username}"
                password = "${artifactory_password}"
                maven = true
            }
            defaults {
                publications ('mavenJava')
            }
        }
    }

    publishing {

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("${github_packages_url}")
                credentials {
                    username = System.getenv("GH_PACKAGES_USERNAME")
                    password = System.getenv("GH_PACKAGES_TOKEN")
                }
            }
        }

        publications {
            mavenJava(MavenPublication) {
                from components.java
            }
        }
    }

    sonarqube {
        properties {
            property 'sonar.sources', 'src/main/java'
            property 'sonar.exclusions', '**/*Test*/**'
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }
}

task version() {
    doLast {
        println project.version
    }
}